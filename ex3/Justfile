set shell := ["bash", "-c"]

RAW_BUCKET := "reviews-raw-bucket"
PROCESSED_BUCKET := "reviews-processed-bucket"
SENTIMENT_INPUT_BUCKET := "reviews-sentiment-input-bucket"
CUSTOMER_TABLE := "review-customers"
RESULTS_TABLE := "review-results"
LAMBDA_ROLE_NAME := "review-processing-lambda-role"
LAMBDA_ROLE_ARN := "arn:aws:iam::000000000000:role/review-processing-lambda-role"

setup: s3 dynamo ssm roles lambdas triggers

s3:
    awslocal s3 mb s3://{{ RAW_BUCKET }}
    awslocal s3 mb s3://{{ PROCESSED_BUCKET }}
    awslocal s3 mb s3://{{ SENTIMENT_INPUT_BUCKET }}

dynamo:
    awslocal dynamodb create-table \
    --table-name {{ CUSTOMER_TABLE }} \
    --attribute-definitions AttributeName=reviewerID,AttributeType=S \
    --key-schema AttributeName=reviewerID,KeyType=HASH \
    --billing-mode PAY_PER_REQUEST || echo "Table {{ CUSTOMER_TABLE }} already exists."

    awslocal dynamodb create-table \
        --table-name {{ RESULTS_TABLE }} \
        --attribute-definitions AttributeName=review_id,AttributeType=S \
        --key-schema AttributeName=review_id,KeyType=HASH \
        --billing-mode PAY_PER_REQUEST || echo "Table {{ RESULTS_TABLE }} already exists."

ssm:
    awslocal ssm put-parameter --name "/localstack-review-app/tables/customers" --type "String" --value {{ CUSTOMER_TABLE }} --overwrite
    awslocal ssm put-parameter --name "/localstack-review-app/tables/results" --type "String" --value {{ RESULTS_TABLE }} --overwrite
    awslocal ssm put-parameter --name "/localstack-review-app/buckets/reviews" --type "String" --value {{ RAW_BUCKET }} --overwrite

roles:
    echo $(awslocal iam create-role \
      --role-name {{ LAMBDA_ROLE_NAME }} \
      --assume-role-policy-document '{"Version": "2012-10-17","Statement": [{ "Effect": "Allow", "Principal": {"Service": "lambda.amazonaws.com"}, "Action": "sts:AssumeRole"}]}' \
      --query 'Role.Arn' --output text || awslocal iam get-role --role-name {{ LAMBDA_ROLE_NAME }} --query 'Role.Arn' --output text)

    awslocal iam attach-role-policy --role-name {{ LAMBDA_ROLE_NAME }} --policy-arn "arn:aws:iam::aws:policy/service-role/AWSLambdaBasicExecutionRole"
    awslocal iam attach-role-policy --role-name {{ LAMBDA_ROLE_NAME }} --policy-arn "arn:aws:iam::aws:policy/AmazonS3FullAccess"
    awslocal iam attach-role-policy --role-name {{ LAMBDA_ROLE_NAME }} --policy-arn "arn:aws:iam::aws:policy/AmazonDynamoDBFullAccess"
    awslocal iam attach-role-policy --role-name {{ LAMBDA_ROLE_NAME }} --policy-arn "arn:aws:iam::aws:policy/AmazonSSMFullAccess"
    echo "Waiting for IAM role propagation..."
    sleep 10

lambdas:
    awslocal lambda delete-function --function-name presign &>/dev/null || true
    (cd lambdas/presign; rm -f lambda.zip; zip lambda.zip handler.py)
    awslocal lambda create-function \
      --function-name presign \
      --runtime python3.11 --timeout 10 \
      --zip-file fileb://lambdas/presign/lambda.zip \
      --handler handler.handler \
      --role {{ LAMBDA_ROLE_ARN }} \
      --environment "Variables={STAGE=local}"
    awslocal lambda create-function-url-config --function-name presign --auth-type NONE

    # --- Preprocessing Lambda ---
    awslocal lambda delete-function --function-name preprocessing &>/dev/null || true
    (cd lambdas/preprocessing; rm -rf lambda.zip package; mkdir package; pip install -r requirements.txt -t package; cd package; zip -r ../lambda.zip .; cd ..; zip lambda.zip handler.py)
    awslocal lambda create-function \
      --function-name preprocessing \
      --runtime python3.11 --timeout 10 \
      --zip-file fileb://lambdas/preprocessing/lambda.zip \
      --handler handler.preprocess \
      --role {{ LAMBDA_ROLE_ARN }} \
      --environment "Variables={SOURCE_BUCKET={{ RAW_BUCKET }},DESTINATION_BUCKET={{ PROCESSED_BUCKET }},STOPWORDS_KEY=stopwords.txt}"

    # --- Profanity Check Lambda ---
    awslocal lambda delete-function --function-name profanity-check &>/dev/null || true
    (cd lambdas/profanity-check; rm -rf lambda.zip package; mkdir package; pip install -r requirements.txt -t package; cd package; zip -r ../lambda.zip .; cd ..; zip lambda.zip handler.py)
    awslocal lambda create-function \
      --function-name profanity-check \
      --runtime python3.11 --timeout 10 \
      --zip-file fileb://lambdas/profanity-check/lambda.zip \
      --handler handler.handler \
      --role {{ LAMBDA_ROLE_ARN }} \
      --environment "Variables={DESTINATION_BUCKET={{ SENTIMENT_INPUT_BUCKET }}}"

    # --- Sentiment Analysis Lambda ---
    awslocal lambda delete-function --function-name sentiment-analysis &>/dev/null || true
    (cd lambdas/sentiment-analysis; rm -rf lambda.zip package; mkdir package; pip install -r requirements.txt -t package; cd package; zip -r ../lambda.zip .; cd ..; zip lambda.zip handler.py)
    awslocal lambda create-function \
      --function-name sentiment-analysis \
      --runtime python3.11 --timeout 10 \
      --zip-file fileb://lambdas/sentiment-analysis/lambda.zip \
      --handler handler.handler \
      --role {{ LAMBDA_ROLE_ARN }}

triggers:
    # Trigger for Preprocessing and Sentiment Analysis
    awslocal s3api put-bucket-notification-configuration \
      --bucket {{ RAW_BUCKET }} \
      --notification-configuration "{\"LambdaFunctionConfigurations\":[{\"LambdaFunctionArn\":\"arn:aws:lambda:us-east-1:000000000000:function:preprocessing\",\"Events\":[\"s3:ObjectCreated:*\"]}, {\"LambdaFunctionArn\":\"arn:aws:lambda:us-east-1:000000000000:function:sentiment-analysis\",\"Events\":[\"s3:ObjectCreated:*\"]}]}"

    # Trigger for Profanity Check
    awslocal s3api put-bucket-notification-configuration \
      --bucket {{ PROCESSED_BUCKET }} \
      --notification-configuration "{\"LambdaFunctionConfigurations\":[{\"LambdaFunctionArn\":\"arn:aws:lambda:us-east-1:000000000000:function:profanity-check\",\"Events\":[\"s3:ObjectCreated:*\"]}]}"
